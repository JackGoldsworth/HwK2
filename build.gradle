import org.jetbrains.gradle.ext.*

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'antlr'
    id "org.sonarqube" version "2.7"
    id 'application'
    id 'idea'
    id "org.jetbrains.gradle.plugin.idea-ext" version "0.7"
}

group 'me.jackgoldsworth'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    antlr "org.antlr:antlr4:4.7.2"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile group: 'org.ow2.asm', name: 'asm', version: '7.2'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileKotlin.dependsOn generateGrammarSource

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-long-messages", "-package", "me.jackgoldsworth.hwk2.parser"]
    outputDirectory = new File("src/main/java/me/jackgoldsworth/hwk2/parser/".toString())
}

sonarqube {
    properties {
        property "sonar.projectName", "HwK2"
        property "sonar.projectKey", "JackGoldsworth_HwK2"

        property "sonar.links.homepage", "https://github.com/JackGoldsworth/HwK2"
        property "sonar.links.ci", "https://travis-ci.com/JackGoldsworth/HwK2"
        property "sonar.links.scm", "https://github.com/JackGoldsworth/HwK2"
        property "sonar.links.issue", "https://github.com/JackGoldsworth/HwK2/issues"

    }
}

// https://github.com/JetBrains/gradle-idea-ext-plugin/blob/master/src/main/groovy/org/jetbrains/gradle/ext/RunConfigurations.groovy
idea {
    project {
        settings {
            runConfigurations {
                "Compile"(Application) {
                    def fileURL = project.file('src/main/resources/example.hwk')
                    programParameters = fileURL.absolutePath
                    mainClass = 'me.jackgoldsworth.hwk2.CompilerKt'
                    moduleName = 'HwK2.main'
                }
            }
        }
    }
}